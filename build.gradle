plugins {
    id 'java'
    id 'idea'
    id 'ninja.miserable.blossom' version '1.0.1'
}

group 'com.aniskywalker'
version '1.0-SNAPSHOT'

ext.url = 'https://github.com/20zinnm/Skyblock'
description = 'A modular, advanced Skyblock plugin for Sponge.'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.spongepowered:spongeapi:5.+'
    // Just a build tool, chill guys.
    compile "org.projectlombok:lombok:1.16.10"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.2'
    compile group: 'com.rethinkdb', name: 'rethinkdb-driver', version: '2.3.3'
}

blossom {

    def location = 'src/main/java/com/aniskywalker/skyblock/core/util/PluginInfo.java'

    replaceToken '@name@', project.name, location
    replaceToken '@version@', project.version, location
    replaceToken '@gitHash@', getGitHash(), location
    replaceToken '@description@', project.description, location
    replaceToken '@url@', project.url, location

}

def private String getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute();
    process.waitFor();
    return process.exitValue() ? 'unknown' : process.text.trim();
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}